#.rst:
# MsgPack
# -------
#
# The following import targets are created
#
# ::
#
#   MsgPack::msgpack-c
#   MsgPack::msgpack-c-shared (@MSGPACK_ENABLE_SHARED@)
#   MsgPack::msgpack-c-static
#   MsgPack::msgpack-cpp
#

@PACKAGE_INIT@

include(CMakeFindDependencyMacro)

if(NOT TARGET msgpack-c-shared AND NOT TARGET msgpack-c-static AND NOT TARGET msgpack-cpp)
  include("${CMAKE_CURRENT_LIST_DIR}/MsgPackTargets.cmake")

  if(@MSGPACK_ENABLE_SHARED@)
    add_library(MsgPack::msgpack-c ALIAS MsgPack::msgpack-c-shared)
  else()
    add_library(MsgPack::msgpack-c ALIAS MsgPack::msgpack-c-static)
  endif()
else()
  if(TARGET msgpack-c-shared)
    add_library(MsgPack::msgpack-c ALIAS msgpack-c-shared)
  else()
    add_library(MsgPack::msgpack-c ALIAS msgpack-c-static)
  endif()
  add_library(MsgPack::msgpack-c-static ALIAS msgpack-c-static)
  if(TARGET msgpack-cpp)
    add_library(MsgPack::msgpack-cpp ALIAS msgpack-cpp)
  endif()
endif()
